#!/bin/sh
//usr/bin/env stap -I`dirname $0`/../tapset $@ $0; exit $? 
# SL -- run a steam locomotion in terminal
# Copyright (C) 2011 Masami Hiramatsu <masami.hiramatsu@gmail.com>
#
# This file is free software and is distributed under the terms of the GNU
# General Public License (GPL); either version 2, or (at your option) any
# later version.

global pos_x, pos_y
global sl[5], slen

probe begin {
	sl[0] = "     *o*o*o*o       "
	sl[1] = "    *_ =====    ___ "
	sl[2] = " ___||_|[_]|  _/   |"
	sl[3] = "{ ______[_]|=|_____|"
	sl[4] = "/|OO==OO=OO   O   O "
	slen = strlen(sl[0])

	pos_x = game_tty_ws_col() + slen
	pos_y = game_tty_ws_row() / 2 - 3
}

probe timer.ms(60) {
	col = game_tty_ws_col()
	row = game_tty_ws_row()
	if (pos_y + 5 > row) pos_y = row - 5

	x = (pos_x >= 1) ? pos_x : 1
	s = x - pos_x;
	e = (pos_x + slen > col) ? col - pos_x : slen 

	ansi_clear_screen()
	foreach (i in sl) {
		ansi_cursor_move(x, pos_y + i)
		print(substr(sl[i], s, e - s))
	}
	pos_x --;
	if (pos_x < -slen)
		exit()
}


