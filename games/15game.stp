#!/bin/sh
//usr/bin/env stap -I `dirname $0`/../tapset $@ $0; exit $?
# 15 game
# Copyright (C) 2008 Masami Hiramatsu <masami.hiramatsu@gmail.com>
#
# This file is free software and is distributed under the terms of the GNU
# General Public License (GPL); either version 2, or (at your option) any
# later version.


global m[16], x, t

function move(k:long) {
  d = ((k&1)?-1:1)*((k&2)?4:1)
  if (!(k&2) && (x/4 != (x+d)/4))
    return 0
  if (0 <= x+d && x+d < 16) {
    m[x] = m[x+d]; m[x+d] = 0
    x+=d
  }
}

function show() {
  cursor_cls()
  for (i=0;i<15;i+=4)
    printf("|%02d|%02d|%02d|%02d|\n",m[i],m[i+1],m[i+2],m[i+3])
}

probe begin {
  game_srand(gettimeofday_ns())
  for (i=0; i<15; i++)
    m[i]=i+1
  m[i]=0; x=i
  for (i=0;i<50;i++)
    move(game_urand()%4)
  show()
  t=gettimeofday_ms()
}

probe game.kbd.down {
  if (code == GM_KBD_DOWN) {
    move(2)
  } else if (code == GM_KBD_RIGHT) {
    move(0)
  } else if (code == GM_KBD_LEFT) {
    move(1)
  } else if (code == GM_KBD_UP) {
    move(3)
  }
  show()
  for (i=0;i<15;i++)
    if(m[i]!=i+1) next
  exit()
}

probe end {
  t = gettimeofday_ms() - t
  printf("Congraturations! Time:%d.%03ds\n",t/1000,t%1000)
}
